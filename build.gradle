// vim: set ft=groovy:
// http://stackoverflow.com/questions/1163173/why-use-gradle-instead-of-ant-or-maven

// ***************
// * Gradle setup
// ***************

// Sources for the build script itself
buildscript {
   repositories {
      mavenCentral()
   }

   dependencies {
      classpath "com.admc:gradle-javaPropFile-plugin:latest.integration"
   }
} 

// Plugins
apply plugin: "base"
apply plugin: "eclipse"
apply plugin: "javaPropFile"

// Imports
import groovy.swing.SwingBuilder
import javax.swing.JFrame

// This loads configuration from app.properties and overrides with local.properties
// See: https://github.com/unsaved/gradle-javaPropFile-plugin/tree/master/doc#void-propfileloadertraditionalpropertiesinit
propFileLoader.traditionalPropertiesInit()  


// ************************
// * Project configuration
// ************************

// Common setup for subprojects
subprojects {
   apply plugin: "java"
   apply plugin: "maven"
   apply plugin: 'signing'

   group = "com.googlecode.cedar-common"
   buildDir = "../build/${project.name}"
   
   task libraryJar(type: Copy) {
      outputs.upToDateWhen { false }
      from(".")
      into(file(buildDir.canonicalPath + "/libs").canonicalPath)
      include("${project.name}-${version}.jar")
   }

   // this tricks gradle into generating the jar by executing libraryJar
   tasks.jar.dependsOn(libraryJar)
   jar.enabled = false

   uploadArchives {
       repositories {
           mavenDeployer {
               repository(url: config_mavenPublishRepositoryUrl)
               beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) } 
           }
       }
   }

   install { }

   signing {
      required = { gradle.taskGraph.hasTask(":${project.name}:uploadArchives") }
      sign jar
   }

   artifacts {
      jar
   }

   task publish(dependsOn: uploadArchives)

   // force the signJar task to run every time, even if it thinks it's up-to-date
   tasks.matching { task -> task.name == "signJar" }.all {
      outputs.upToDateWhen { false }
   }

}

// Subproject for gwt-sl dependency
project(":gwt-sl") {

   version = "1.2"

   uploadArchives {
       repositories {
           mavenDeployer {
               pom.artifactId = "${project.name}"
               pom.version = "${version}"
               pom.project {
                  name "GWT Server Library (Repackaged)"
                  description "*** THIS IS AN OBSOLETE VERSION UPLOADED FOR USE WITH CEDAR-COMMON.*** " +
                              "The Server Library for GWT is a collection of Java server " +
                              "side components for the Google Web Toolkit AJAX framework " +
                              "with the focus on the Spring framework by facilitating " + 
                              "publishing of Spring beans as RPC services with support " +
                              "for Hibernate-managed entities."
                  url "https://code.google.com/p/gwt-sl/"
                  scm {
                     url "scm:https://code.google.com/p/cedar-common.dependencies/"
                     connection "scm:hg:https://code.google.com/p/cedar-common.dependencies/"
                     developerConnection "scm:hg:https://code.google.com/p/cedar-common.dependencies/"
                  }
                  licenses {
                     license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/license/LICENSE-2.0.txt"
                        distribution "repo"
                     }
                  }
                  developers {
                     developer {
                        id "author"
                        name "George Georgovassilis"
                        email "g.georgovassilis@gmail.com"
                     }
                  }
               }
           }
       }
   }

}

// Subproject for cpsuite dependency
project(":cpsuite") {

   version = "1.2.6"

   // this tricks gradle into generating the jar by executing libraryJar
   tasks.jar.dependsOn(libraryJar)
   jar.enabled = false

   uploadArchives {
       repositories {
           mavenDeployer {
               pom.artifactId = "${project.name}"
               pom.version = "${version}"
               pom.project {
                  name "Classpath Suite (Repackaged)"
                  description "*** THIS VERSION UPLOADED FOR USE WITH CEDAR-COMMON.*** " +
                              "Dynamically generates JUnit4 test suites."
                  url "http://johanneslink.net/projects/cpsuite.jsp"
                  scm {
                     url "scm:https://code.google.com/p/cedar-common.dependencies/"
                     connection "scm:hg:https://code.google.com/p/cedar-common.dependencies/"
                     developerConnection "scm:hg:https://code.google.com/p/cedar-common.dependencies/"
                  }
                  licenses {
                     license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/license/LICENSE-2.0.txt"
                        distribution "repo"
                     }
                  }
                  developers {
                     developer {
                        id "author"
                        name "Johannes Link"
                        email "business@johanneslink.net"
                     }
                  }
               }
           }
       }
   }

}


// *********************
// * Digital signatures
// *********************

// Set all of the signature configuration, for all projects
def setSignatureConfiguration(passphrase) {
   allprojects { ext."signing.keyId" = config_gpgKeyId }
   allprojects { ext."signing.secretKeyRingFile" = config_gpgSecretKey }
   allprojects { ext."signing.password" = passphrase }
}

// Get the GPG passphrase from the user when it's needed 
gradle.taskGraph.whenReady { taskGraph ->
   if (taskGraph.allTasks.any { it instanceof Sign && it.required }) {
      Console console = System.console()
      if (console != null) {
         getPassphraseViaConsole()
      } else {
         getPassphraseViaPopup()
      }
   }
}

// Get the GPG passphrase via the system console 
def getPassphraseViaConsole() {
   Console console = System.console()
   def passphrase = console.readPassword("\n\nEnter passphrase for GPG key ${config_gpgKeyId}: ")
   setSignatureConfiguration(passphrase)
}

// Get the GPG passphrase via a GUI pop-up, if the console is not available
def getPassphraseViaPopup() {
   boolean alive = true

   def swing = new SwingBuilder()
   def button = swing.button('OK')
   def label = swing.label("Enter passphrase: ")
   def passphrase = null

   def frame = swing.frame(title: "GPG Key ${config_gpgKeyId}", defaultCloseOperation: JFrame.EXIT_ON_CLOSE) {
      panel {
         widget(label)
         passphrase = passwordField(columns:18)
         widget(button)
      }
   }

   frame.setLocationRelativeTo(null)

   button.actionPerformed = {
      setSignatureConfiguration(passphrase.text)
      alive = false
   }

   passphrase.actionPerformed = {
      setSignatureConfiguration(passphrase.text)
      alive = false
   }

   frame.pack()
   frame.show()

   while (alive) {
      sleep(1000)
   }

   frame.hide()
}


// ************************
// * Eclipse configuration
// ************************
// The eclipse plugin is used to generate Eclipse project files

// Hide the various derived directories from Eclipse
project.eclipse.project.file.withXml { provider ->
   ignoreResourcesFromDirectories(provider, ["build", ])
} 

// Ignore resources in a specific directory
// See: http://forums.gradle.org/gradle/topics/eclipse_generated_files_should_be_put_in_the_same_place_as_the_gradle_generated_files
def ignoreResourcesFromDirectories(provider, directories) {
   def filter = provider.asNode().appendNode("filteredResources").appendNode("filter")
   filter.appendNode("id", String.valueOf(System.currentTimeMillis()))  // this id must be unique
   filter.appendNode("name")
   filter.appendNode("type", "26")
   def matcher = filter.appendNode("matcher")
   matcher.appendNode("id", "org.eclipse.ui.ide.orFilterMatcher")
   def arguments = matcher.appendNode("arguments")
   directories.each {
      def dirMatcher = arguments.appendNode("matcher")
      dirMatcher.appendNode("id", "org.eclipse.ui.ide.multiFilter")
      dirMatcher.appendNode("arguments", "1.0-projectRelativePath-matches-true-false-${it}")
   }
} 

